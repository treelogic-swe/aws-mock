allprojects {
    repositories {
        maven { url 'http://repo2.maven.org/maven2/' }
    }
}

buildscript {
    repositories {
        maven { url 'http://repo2.maven.org/maven2/' }
    }
    dependencies {
        classpath "net.saliman:gradle-cobertura-plugin:2.3.2"
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse-wtp'
apply plugin: 'jetty'
apply plugin: 'checkstyle'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: "net.saliman.cobertura"

group = 'com.treelogic-swe'
version = '1.2.0'
ext.packaging = 'jar'

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

[jettyRun,jettyRunWar]*.daemon = false
[jettyRun,jettyRunWar]*.httpPort = 8000
[jettyRun,jettyRunWar,jettyStop]*.stopPort = 38091   // Port for stop signal
[jettyRun,jettyRunWar,jettyStop]*.stopKey = 'foo'

// fix the issue that jetty not stops in daemon mode
import org.gradle.api.plugins.jetty.internal.Monitor
[jettyRun, jettyRunWar]*.doLast {
   if (getStopPort() != null && getStopPort() > 0 && getStopKey() != null) {
      Monitor monitor = new Monitor(getStopPort(), getStopKey(), server.getProxiedObject());
      monitor.start();
   }
}

dependencies {
    providedCompile 'javax.servlet:servlet-api:2.5'
    compile 'org.apache.commons:commons-lang3:3.1'
    compile 'org.slf4j:slf4j-log4j12:1.7.5'
    compile 'log4j:log4j:1.2.17'
    compile 'org.freemarker:freemarker:2.3.20'
    compile 'com.treelogic-swe:cxf-stub:1.0.4'
    compile group: 'joda-time', name: 'joda-time', version: '2.8.1'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.0.1'
    
    testCompile "com.amazonaws:aws-java-sdk:1.11.52"
    testCompile "org.powermock:powermock:1.6.5"
    testCompile "org.powermock:powermock-module-junit4:1.6.5"
    testCompile "org.powermock:powermock-api-mockito:1.6.5"
}

// fix the issue that in eclipse workspace source/javadoc of dependency libs don't show correctly
eclipse.classpath.file {
    withXml { xml ->
        def node = xml.asNode()
        node.remove( node.find { it.@path == 'org.eclipse.jst.j2ee.internal.web.container' } )
        node.appendNode( 'classpathentry', [ kind: 'con', path: 'org.eclipse.jst.j2ee.internal.web.container', exported: 'true'])
    }
}

sourceSets {
    // example code also in java source sets
    examples {
        java {
            srcDir 'example/java/simple'
            srcDir 'example/java/full/client-usage'
            srcDir 'example/java/full/extending-aws-mock/java'
            srcDir 'example/java/test'
        }
        compileClasspath += sourceSets.main.runtimeClasspath
    }

    test {
        compileClasspath += sourceSets.examples.runtimeClasspath
        runtimeClasspath += sourceSets.examples.runtimeClasspath
    }

    // integration test code are separated from src/test/java
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    examplesCompile.extendsFrom testCompile
    examplesRuntime.extendsFrom testRuntime
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
   testClassesDir = sourceSets.integrationTest.output.classesDir
   classpath = sourceSets.integrationTest.runtimeClasspath
   doFirst {
       jettyRun.daemon = true
       jettyRun.httpPort = 8001
       [jettyRun,jettyStop]*.stopPort = 38092
       [jettyRun,jettyStop]*.stopKey = 'bar'
       jettyRun.execute()
   }
   doLast {
       jettyStop.execute()
   }
}

check.dependsOn integrationTest

checkstyle {
    sourceSets = [ sourceSets.main, sourceSets.examples ]
}

task checkstylejs (type: Checkstyle) {
    source 'example/nodejs'
    include '**/*.js'
    classpath=files()
}

task sourcesJar(type:Jar, dependsOn: classes){
    classifier = 'sources'
    from sourceSets.main.allSource
    exclude "**/log4j.properties"
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}


task run(dependsOn: jettyRun) {

}

test {
    testLogging.showStandardStreams = false
}

jar {
    exclude('log4j.properties')
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

/**
 * We comment the signing task for avoiding travis-ci build failure
 * because we don't commit secret key for signing jars to github.com.
 */
//signing {
//    sign configurations.archives
//}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment)  }

            /**
             * for publishing to local folder - for normal travis-ci builds
             * run with signing task commented
             */
            repository(url: "file:/tmp/tlswe/repo")

            /**
             * for publishing to public maven repo - for manually releasing to maven repo
             * run with signing task uncommented
             */
            //repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            //  authentication(userName: sonatypeUsername, password: sonatypePassword)
            //}

            addFilter('upload_jars') {artifact, file ->
                 artifact.type != 'war'
            }
 
            pom('upload_jars').project {
               name 'aws-mock'
               packaging 'jar'
               description 'A lightweight, language-agnostic mock of essential AWS services, for test automation.'
               url 'https://github.com/treelogic-swe/aws-mock'
 
               scm {
                   url 'scm:git@github.com:treelogic-swe/aws-mock.git'
                   connection 'scm:git@github.com:treelogic-swe/aws-mock.git'
                   developerConnection 'scm:git@github.com:treelogic-swe/aws-mock.git'
               }
 
               licenses {
                   license {
                       name 'MIT'
                       distribution 'repo'
                   }
               }
 
               developers {
                   developer {
                        id 'christopherbalz'
                        name 'Christopher Balz'
                        email 'christopherbalz@yahoo.com'
                    }
                    developer {
                        id 'maxiaohao'
                        name 'Ma Xiaohao'
                        email 'maxiaohao@gmail.com'
                    }
                    developer {
                        id 'Jayscrivner'
                        name 'Jay Scrivner'
                        email 'Jayscrivner@gmail.com'
                    }
                    developer {
                        id 'zkareem'
                        name 'Zaki Kareem'
                        email 'zakimak9@gmail.com'
                    }
               }
            }
            
        }  
    }
}

cobertura {
    coverageCheckHaltOnFailure = true
    coverageCheckBranchRate = 0
    coverageCheckLineRate = 0
    coverageCheckPackageBranchRate = 0
    coverageCheckPackageLineRate = 0
    coverageCheckTotalLineRate = 80
    coverageCheckTotalBranchRate = 80
    coverageReportDir = file("$buildDir/cobertura/")
}

